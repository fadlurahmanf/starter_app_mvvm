plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'com.google.gms.google-services'
}

def localProperties = new Properties()
localProperties.load(new FileInputStream(rootProject.file("local.properties")))
def BASE_QURAN_URL = localProperties.getProperty("BASE_QURAN_URL")
def DEV_URL = localProperties.getProperty("DEV_URL")
def STAGING_URL = localProperties.getProperty("STAGING_URL")
def PRODUCTION_URL = localProperties.getProperty("PRODUCTION_URL")
def IDENTITY_PREFIX = localProperties.getProperty("IDENTITY_PREFIX")
def CIF_PREFIX = localProperties.getProperty("CIF_PREFIX")
def PUBLIC_KEY_DEV = localProperties.getProperty("PUBLIC_KEY_DEV")
def PRIVATE_KEY_DEV = localProperties.getProperty("PRIVATE_KEY_DEV")
def PUBLIC_KEY_STAGING = localProperties.getProperty("PUBLIC_KEY_STAGING")
def PRIVATE_KEY_STAGING = localProperties.getProperty("PRIVATE_KEY_STAGING")
def PUBLIC_KEY_PRODUCTION = localProperties.getProperty("PUBLIC_KEY_PRODUCTION")
def PRIVATE_KEY_PRODUCTION = localProperties.getProperty("PRIVATE_KEY_PRODUCTION")

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion 33
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.fadlurahmanf.starterappmvvm"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 6
        versionName "0.1.0"
        multiDexEnabled true

        buildConfigField "String", "BASE_QURAN_URL", BASE_QURAN_URL
        buildConfigField "String", "IDENTITY_PREFIX", IDENTITY_PREFIX
        buildConfigField "String", "CIF_PREFIX", CIF_PREFIX

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildFeatures{
        viewBinding true
    }

    signingConfigs {
        release {
            storeFile file('../keystore.jks')
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
        }
    }
    flavorDimensions += "version"
    productFlavors {
        dev {
            applicationIdSuffix ".dev"
            buildConfigField "String", "BASE_URL", DEV_URL
            buildConfigField "String", "PUBLIC_KEY", PUBLIC_KEY_DEV
            buildConfigField "String", "PRIVATE_KEY", PRIVATE_KEY_DEV
        }
        staging {
            applicationIdSuffix ".staging"
            buildConfigField "String", "BASE_URL", STAGING_URL
            buildConfigField "String", "PUBLIC_KEY", PUBLIC_KEY_STAGING
            buildConfigField "String", "PRIVATE_KEY", PRIVATE_KEY_STAGING
        }
        production {
            buildConfigField "String", "BASE_URL", PRODUCTION_URL
            buildConfigField "String", "PUBLIC_KEY", PUBLIC_KEY_PRODUCTION
            buildConfigField "String", "PRIVATE_KEY", PRIVATE_KEY_PRODUCTION
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.core:core-ktx:1.9.0"
    implementation "androidx.appcompat:appcompat:1.5.1"
    implementation "com.google.android.material:material:1.7.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"

    // testing
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.8.2")
    testImplementation("org.junit.jupiter:junit-jupiter-engine:5.8.2")

    //mockito
    testImplementation "org.mockito:mockito-inline:4.7.0"
    testImplementation "androidx.arch.core:core-testing:2.2.0"
    testImplementation("org.mockito.kotlin:mockito-kotlin:4.0.0")
    testImplementation("org.powermock:powermock-mockito-release-full:1.5.4")

    // for testing shared preference
    testImplementation 'io.github.ivanshafran:shared-preferences-mock:1.2.4'

    androidTestImplementation "androidx.test.ext:junit:1.1.4"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.0"


    //dagger
    implementation "com.google.dagger:dagger:2.44"
    kapt "com.google.dagger:dagger-compiler:2.44"

    // network
    var retrofit_version = "2.9.0"
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.3"
    implementation "com.squareup.retrofit2:retrofit:${retrofit_version}"
    implementation "com.squareup.retrofit2:converter-gson:${retrofit_version}"
    implementation "com.squareup.retrofit2:adapter-rxjava3:${retrofit_version}"

    // rx
    implementation "io.reactivex.rxjava3:rxandroid:3.0.0"
    implementation "io.reactivex.rxjava3:rxjava:3.1.1"

    // work
    implementation "androidx.work:work-runtime-ktx:2.7.1"

    // glide
    implementation "com.github.bumptech.glide:glide:4.14.2"
    kapt "com.github.bumptech.glide:compiler:4.14.2"

    implementation "com.github.barteksc:android-pdf-viewer:2.8.2"

    implementation "com.github.MikeOrtiz:TouchImageView:3.2.1"

    // room dao
    implementation("androidx.room:room-runtime:$room_version")
    kapt("androidx.room:room-compiler:$room_version")
    implementation("androidx.room:room-ktx:$room_version")
    implementation("androidx.room:room-paging:$room_version")
    implementation "androidx.room:room-rxjava3:$room_version"

    // web rtc
    implementation("org.webrtc:google-webrtc:1.0.32006")
    implementation "org.java-websocket:Java-WebSocket:1.5.3"

    // chucker
    debugImplementation "com.github.chuckerteam.chucker:library:3.5.2"
    releaseImplementation "com.github.chuckerteam.chucker:library-no-op:3.5.2"

    def lifecycle_version = "2.5.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    def camerax_version = "1.2.0"
    implementation "androidx.camera:camera-camera2:$camerax_version"
    implementation "androidx.camera:camera-lifecycle:$camerax_version"
    // todo: check later -> if camera supported
    implementation "androidx.camera:camera-view:1.0.0-alpha28"
    implementation "com.google.mlkit:barcode-scanning:17.0.3"

    implementation "com.google.firebase:firebase-messaging:23.1.1"

    def exoplayer_version = "2.18.2"
    implementation "com.google.android.exoplayer:exoplayer-core:$exoplayer_version"
    implementation "com.google.android.exoplayer:exoplayer-dash:$exoplayer_version"
    implementation "com.google.android.exoplayer:exoplayer-ui:$exoplayer_version"
    implementation "com.google.android.exoplayer:exoplayer:$exoplayer_version"

    implementation 'com.github.rubensousa:previewseekbar:3.1.0'
    implementation 'com.github.rubensousa:previewseekbar-exoplayer:2.18.1.0'
    implementation 'com.github.wseemann:FFmpegMediaMetadataRetriever-core:1.0.19'
    implementation 'com.github.wseemann:FFmpegMediaMetadataRetriever-native:1.0.19'

    implementation platform('com.google.firebase:firebase-bom:31.2.3')
    implementation 'com.google.firebase:firebase-config-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'


    implementation 'com.appsflyer:af-android-sdk:6.9.0'
    implementation 'com.facebook.android:facebook-android-sdk:[4,5)'

    implementation("org.bouncycastle:bcprov-jdk15on:1.70")
}